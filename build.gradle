buildscript {
    repositories {
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.3'
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.4'
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.40.4'
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.17.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.5.31' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.5.31' apply false
    id 'org.sonarqube' version '3.3'
}

sonarqube {
    properties {
        property 'sonar.android.lint.report', 'build/reports/lint-results.xml'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()

        maven {
            url 'https://maven.pkg.github.com/sukawasatoru/android-logger'
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
}

def setupAndroidPlugin = { project ->
    project.android {
        compileSdk 31
        buildToolsVersion '31.0.0'

        defaultConfig {
            minSdk 28
            targetSdk 31
        }

        compileOptions {
            coreLibraryDesugaringEnabled true
            sourceCompatibility JavaVersion.VERSION_11
            targetCompatibility JavaVersion.VERSION_11
        }

        signingConfigs {
            config {
                enableV3Signing = true
                enableV4Signing = true
            }
        }
    }

    project.dependencies {
        coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    }
}

def setupKotlinAndroidPlugin = { project ->
    project.android {
        sourceSets {
            androidTest.java.srcDirs += 'src/androidTest/kotlin'
            debug.java.srcDirs += 'src/debug/kotlin'
            main.java.srcDirs += 'src/main/kotlin'
            test.java.srcDirs += 'src/test/kotlin'
            testDebug.java.srcDirs += 'src/testDebug/kotlin'
        }

        kotlinOptions {
            freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
            jvmTarget = JavaVersion.VERSION_11
        }
    }
}

def setupSpotlessPlugin = { Project project ->
    project.spotless {
        kotlin {
            target project.fileTree(dir: 'src', include: '**/*.kt', exclude: ['test/**/*.kt', 'androidTest/**/*.kt'])
            ktlint('0.43.0').userData([
                android: 'true',
                max_line_length: '120',
            ])
        }
    }
}

subprojects {
    project.plugins.whenPluginAdded {
        switch (it.class.name) {
            case 'com.android.build.gradle.AppPlugin':
            case 'com.android.build.gradle.LibraryPlugin':
                setupAndroidPlugin(project)
                break
            case 'org.jetbrains.kotlin.gradle.plugin.KotlinAndroidPluginWrapper':
                setupKotlinAndroidPlugin(project)
                break
            case 'com.diffplug.gradle.spotless.SpotlessPlugin':
                setupSpotlessPlugin(project)
                break
            default:
                break
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
